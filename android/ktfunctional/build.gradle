apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile "com.squareup.okhttp3:okhttp:3.8.0"
    compile "org.apache.commons:commons-io:1.3.2"
    //noinspection GradleDependency
    compile "org.apache.commons:commons-compress:1.8.1"
    compile "org.apache.commons:commons-lang3:3.7"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

project.gradle.startParameter.excludedTaskNames.add('lint')
project.gradle.startParameter.excludedTaskNames.add('prepareDebugUnitTestDependencies')
project.gradle.startParameter.excludedTaskNames.add('generateDebugAndroidTestSources')

group = 'com.github.rarnu'
version = '0.1.3'

signing {
    sign configurations.archives
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name project.name
                packaging 'aar'
                description 'KtFunctional'
                url 'https://github.com/rarnu/ktfunctional'

                scm {
                    url 'scm:git@github.com:rarnu/ktfunctional.git'
                    connection 'scm:git@github.com:rarnu/ktfunctional.git'
                    developerConnection 'git@github.com:rarnu/ktfunctional.git'
                }

                licenses {
                    license {
                        name 'GNU General Public License v3.0'
                        url 'https://github.com/rarnu/ktfunctional/blob/master/LICENSE'
                        distribution 'rarnu'
                    }
                }

                developers {
                    developer {
                        id 'rarnu'
                        name 'rarnu'
                        email '123909566@qq.com'
                    }
                }
            }
        }
    }
}
