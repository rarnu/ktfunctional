apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 28

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.squareup.okhttp3:okhttp:3.8.1'
    implementation 'commons-io:commons-io:2.6'
    implementation 'org.apache.commons:commons-compress:1.16.1'
    implementation 'org.apache.commons:commons-lang3:3.7'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

group = 'com.github.rarnu'
version = '0.7.7'

signing {
    sign configurations.archives
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives sourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment)
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }
            pom.project {
                name project.name
                packaging 'aar'
                description 'KtFunctional'
                url 'https://github.com/rarnu/ktfunctional'

                scm {
                    url 'scm:git@github.com:rarnu/ktfunctional.git'
                    connection 'scm:git@github.com:rarnu/ktfunctional.git'
                    developerConnection 'git@github.com:rarnu/ktfunctional.git'
                }

                licenses {
                    license {
                        name 'GNU General Public License v3.0'
                        url 'https://github.com/rarnu/ktfunctional/blob/master/LICENSE'
                        distribution 'rarnu'
                    }
                }

                developers {
                    developer {
                        id 'rarnu'
                        name 'rarnu'
                        email '123909566@qq.com'
                    }
                }
            }
        }
    }
}
